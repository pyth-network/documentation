import { Callout, Tabs, Steps } from 'nextra/components'

# How to Integrate Express Relay as a Protocol

Protocol developers can **permissionlessly** integrate with Express Relay to recapture MEV and access a network of searchers. 

Integrating with Express Relay involves two main steps:

- Update the Protocol's contract to **permission** Express Relay transactions.
- Write a script to **expose** liquidation opportunities to Searchers for auction.

## Update the Protocol's Contract

The Protocol's contract must permit Express Relay to access liquidation opportunities.

<Steps>
### Install the Express Relay SDK

Pyth provides a [Solidity SDK](https://www.npmjs.com/package/@pythnetwork/express-relay-sdk-solidity) to help developers integrate Express Relay into your Protocol. 
The SDK exposes [`IExpressRelay`](https://github.com/pyth-network/pyth-crosschain/blob/main/express_relay/sdk/solidity/IExpressRelay.sol) and [`IExpressRelayFeeReceiver`](https://github.com/pyth-network/pyth-crosschain/blob/main/express_relay/sdk/solidity/IExpressRelayFeeReceiver.sol) interfaces to interact with Express Relay.

<Tabs items={['Hardhat', 'Foundry']}>
  <Tabs.Tab>
  If you are using Hardhat, you can install the SDK using npm:

```bash copy
npm install @pythnetwork/express-relay-sdk-solidity
```
  </Tabs.Tab>
  <Tabs.Tab>
  If you are using Foundry, you must create an NPM project if you don't already have one. From the root directory of your project, run:

```bash copy
npm init -y
npm install @pythnetwork/express-relay-sdk-solidity
```

Then add the following line to `remappings.txt` file:

```text copy
@pythnetwork/express-relay-sdk-solidity/=node_modules/@pythnetwork/express-relay-sdk-solidity
```
  </Tabs.Tab>
</Tabs>

### Modifying the Protocol's Contract

Developers need to update the Protocol's contract to:

1. Utilize [`isPermissioned`](https://github.com/pyth-network/pyth-crosschain/blob/main/express_relay/sdk/solidity/IExpressRelay.sol#L10C14-L10C28) method from `IExpressRelay` interface to **permit** Express Relay transactions.
1. Implement the [`IExpressRelayFeeReceiver`](https://github.com/pyth-network/pyth-crosschain/blob/main/express_relay/sdk/solidity/IExpressRelayFeeReceiver.sol#L4) interface to **receive** funds from Express Relay.

#### 1. Permit Express Relay Transactions

The `isPermissioned` function takes two arguments:
1. `protocolFeeReceiver`: The address of the Protocol's contract.
1. `permissionId`: A unique identifier of a vault or position eligible for liquidation.

```solidity copy
import "@pythnetwork/express-relay-sdk-solidity/IExpressRelay.sol";

// Express Relay contract address on Optimism Sepolia
// Check {INSERT CONTRACT ADDRESS PAGE LINK} for the address deployed on other networks
address expressRelay = 0xD6e417287b875A3932c1Ff5dcB26D4D2C8b90B40;

require(
		IExpressRelay(expressRelay).isPermissioned(
        protocolFeeReceiver,
        permissionId
    ),
		"invalid liquidation"
);
```
<Callout type="info" emoji="ℹ️">
  The `permissionId` represents unique identifying information for the vault or position within a protocol. 
  It can be the vault address or the vault ID, concatenated into `bytes` format. 
  Consult [`Permission Id`](#) for more information on generating permission IDs. 
</Callout>

#### 2. Set up Fee Receiver

After a successful auction, the Express Relay server sends funds to the Protocol's contract. 
Express Relay server calls the `receiveAuctionProceedings` function present in `IExpressRelayFeeReceiver` to send funds to the Protocol's contract

```solidity copy
interface IExpressRelayFeeReceiver {
 function receiveAuctionProceedings(
        bytes calldata permissionKey
    ) external payable;
}
```


The following code snippet shows a sample liquidation method via Express Relay:

```solidity showLineNumbers {1,2,12,14,21,38-42, 57-61} copy
import "@pythnetwork/express-relay-sdk-solidity/IExpressRelay.sol";
import "@pythnetwork/express-relay-sdk-solidity/IExpressRelayFeeReceiver.sol";

struct Vault {
    address tokenCollateral;
    address tokenDebt;
    uint256 amountCollateral;
    uint256 amountDebt;
    uint256 minHealthRatio;
}

contract EasyLend is IExpressRelayFeeReceiver {

    address public immutable expressRelay;

    constructor(
    address expressRelayAddress,
    bool allowUndercollateralized
    ){
    _nVaults = 0;
    expressRelay = expressRelayAddress;
    _allowUndercollateralized = allowUndercollateralized;
    }

    /**
    * @notice liquidate function - liquidates a vault
    * @param vaultID: ID of the vault to be liquidated
    */
    function liquidate(uint256 vaultID) public {
        Vault memory vault = _vaults[vaultID];
        uint256 vaultHealth = _getVaultHealth(vault);
        // Proceed only if the vault health is below the minimum health ratio
        if (vaultHealth >= vault.minHealthRatio) {
            revert InvalidLiquidation();
        }

        // Check if the liquidation is permissioned
        bool permissioned = IExpressRelay(payable(expressRelay)).isPermissioned(
            address(this),
            abi.encode(vaultID) // permissionId generated from the unique vault ID
            );
        require(permissioned, "invalid liquidation");
    
        IERC20(vault.tokenDebt).transferFrom(msg.sender,address(this),vault.amountDebt);
        IERC20(vault.tokenCollateral).transfer(msg.sender,vault.amountCollateral);
    
        _vaults[vaultID].amountCollateral = 0;
        _vaults[vaultID].amountDebt = 0;
    }

    /**
     * @notice receiveAuctionProceedings function - receives the native token from the express relay
     * You can use the permission key to distribute the received funds to users who got liquidated, LPs, etc...
     *
     * @param permissionKey: permission key that was used for the auction
     */
    function receiveAuctionProceedings(
        bytes calldata permissionKey
    ) external payable {
        emit VaultReceivedETH(msg.sender, msg.value, permissionKey);
    }

}
```
</Steps>


## Expose Liquidation Opportunities to Searchers

Protocols must fetch vaults and positions eligible for liquidation and expose them to Express Relay for auction.

The Express Relay auction server provides a **POST** method, `/v1/opportunities`, which accepts a JSON payload containing the details of the liquidation opportunity.

The JSON payload should contain liquidation opportunities in the following format:

```json
{
  "target_calldata": "0xdeadbeef",                                      // Calldata to perform liquidation
  "chain_id": "op_sepolia",                                             
  "target_contract": "0xcA11bde05977b3631167028862bE2a173976CA11",      // Protocol contract address to call for liquidation
  "permission_key": "0xcafebabe",                                       // Unique identifier for the liquidation opportunity
  "target_call_value": "1",                                             // Value(in Wei) to send with to the Protocol contract.
  "buy_tokens": [                                                       // Tokens to buy (Collateral)
    {
      "amount": "1000",
      "token": "0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2"
    }
  ],
  "sell_tokens": [                                                      // Tokens to sell (Oustadaing Debt)
    {
      "amount": "900",
      "token": "0x2260fac5e5542a773aa44fbcfedf7c193bc2c599"
    }
  ],
  "version": "v1"                                                       // Opportunity format version
}
```


Protocols must evaluate each position's health using the latest Oracle prices before exposing them to Express Relay. 
You can do this by indexing the chain, listening to protocol events, or querying open positions through an RPC provider.

Check the [`monitor.ts`]() script, which fetches liquidation opportunities for the below-mentioned [Easy Lend](https://github.com/pyth-network/pyth-crosschain/tree/main/express_relay/examples/easy_lend) example and exposes them to Express Relay for auction.


## Additional Resources

You may find these additional resources helpful for integrating Express Relay as a protocol.

### Example Application

[Easy Lend](https://github.com/pyth-network/pyth-crosschain/tree/main/express_relay/examples/easy_lend) is a simple lending protocol that allows users to borrow and lend assets. This lending protocol contract is updated to permit Express Relay transactions.

### Contract Address

The [Contract Address](./contract-addresses.mdx) page lists the addresses of Express Relay deployed on various networks.

### Error Codes

### API Reference

The [API Reference](https://per-staging.dourolabs.app/docs/) provides detailed information on the Express Relay APIs for submitting liquidation opportunities.